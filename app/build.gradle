apply plugin: "com.android.application"
apply plugin: "realm-android"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

// version info
def versionMajor = 1
def versionMinor = 1
def versionPatch = 1

android {
    compileSdkVersion rootProject.targetSdkVersion

    defaultConfig {
        applicationId "info.juanmendez.breedgallery"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    dataBinding {
        enabled = true

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kapt {
    correctErrorTypes = true
    arguments {
        arg("resourcePackageName", "org.androidannotations.gradle")  //AndroidAnnotations fix
        arg("resourcePackageName", android.defaultConfig.applicationId) //ApplicationId fix
    }
}

// enable @ParametersAreNonnullByDefault annotation. See https://blog.jetbrains.com/kotlin/2017/09/kotlin-1-1-50-is-out/
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

dependencies {

    // Kotlin
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlin_version"

    // Android Support
    implementation "com.android.support:appcompat-v7:$rootProject.buildToolsVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.buildToolsVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"

    // ButterKnife
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // Lifecycle architecture
    implementation "android.arch.lifecycle:extensions:$rootProject.componentsLibVersion"
    implementation "android.arch.lifecycle:viewmodel:$rootProject.componentsLibVersion"

    // Android Annotations
    implementation "org.androidannotations:androidannotations-api:$rootProject.androidAnnotations"
    kapt "org.androidannotations:androidannotations:$rootProject.androidAnnotations"

    // Dagger
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rootProject.rxLifecycleVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:$rootProject.rxLifecycleVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:$rootProject.rxLifecycleVersion"
    implementation 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    // Retrofit and Okhttp
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // Local Unit Test
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.hamcrestVersion"

    // Espresso
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.gradleToolsVersion"
}